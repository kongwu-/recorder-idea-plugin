import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation


plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}
group 'com.github.kongwu'
// Output to build/libs/shadow.jar
shadowJar {
    archiveBaseName.set('agent')
    archiveClassifier.set('')
    archiveVersion.set('')

    //resolve agent class conflict
    relocate 'io.netty', 'com.github.kongwu.recorder.io.netty'
    relocate 'org.apache.commons.lang3', 'com.github.kongwu.recorder.org.apache.commons.lang3'
    relocate 'com.fasterxml.jackson', 'com.github.kongwu.recorder.com.fasterxml.jackson'
}
dependencies {
    implementation project(':common')
    implementation group: 'com.alibaba', name: 'bytekit-core', version: '0.0.4'
    implementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: '1.10.13'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}
jar {
    manifest {

        attributes(
                'Premain-Class': 'com.github.kongwu.recorder.plugin.agent.AgentBootstrap',
                'Agent-Class': 'com.github.kongwu.recorder.plugin.agent.AgentBootstrap',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Can-Set-Native-Method-Prefix': 'true',
                'Implementation-Title': "AgentBootstrap",
                'Implementation-Version': rootProject.version
        )
    }
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"